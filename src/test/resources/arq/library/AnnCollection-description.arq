#QueryScope=Annotation Collection
#QueryReturnType=Graph
#QueryResults=A graph for the given Annotation collection, in the description profile
#QueryParams=R_RES
#QueryUrl=/lib/Resgraph?R_RES=bdr:ANCWCBC2237_AN001

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the URI of the annotation collection

#param.R_RESALIAS.type=resource
#param.R_RESALIAS.subtype=a Resource IRI
#param.R_RESALIAS.desc=the URI of the resulting collection (can be different for subsets)

#param.R_SUBMETHOD.type=resource
#param.R_SUBMETHOD.subtype=a Resource IRI of type subset (to be defined)
#param.R_SUBMETHOD.desc=the URI of the type of subset that should be used to subset the collection

#param.I_SUBRANGEFIRST.type=int
#param.I_SUBRANGEFIRST.subtype=the first index of the range
#param.I_SUBRANGEFIRST.desc=the first index of the range

#param.I_SUBRANGELAST.type=int
#param.I_SUBRANGELAST.subtype=the last index of the range
#param.I_SUBRANGELAST.desc=the last index of the range

CONSTRUCT {
    ?R_RESALIAS ?p ?o .
    ?o1 ?p1 ?o2 .
    ?ann :annInCollection ?R_RES .
    ?ann ?annp ?anno .
    ?annb ?annbp ?annbo .
    ?ant ?antp ?anto .
    ?anto ?antop ?antoo .
    ?annbloc ?annblocp ?annbloco .
    ?annba ?annbap ?annbao .
    ?annbaloc ?annbalocp ?annbaloco .
} where {
    {
        ?R_RES ?p ?o .
    } union {
        ?R_RES ?p1 ?o1 .
        FILTER(isBlank(?o1))
        ?o1 ?p1 ?o2 .
    } union {
        FILTER (?R_SUBMETHOD = bdr:SubsettingNone)
        ?ann :annInCollection ?R_RES .
        ?ann ?annp ?anno .
        ?ann oa:hasTarget ?ant .
        ?ant ?antp ?anto .
        optional {
            FILTER(isBlank(?anto))
            ?anto ?antop ?antoo .
        }
        optional {
          ?ann oa:hasBody ?annb .
          ?annb ?annbp ?annbo .
          optional {
            ?annbo a :WorkLocation ;
            BIND(?annbo as ?annbloc )
            ?annbloc ?annblocp ?annbloco .
          }
          optional {
            ?annbo a adm:Assertion ;
            BIND(?annbo as ?annba )
            ?annba ?annbap ?annbao .
            optional {
               ?annba :workLocation ?annbaloc .
               ?annbaloc ?annbalocp ?annbaloco .
            }
          }
        }
    } union {
        FILTER (?R_SUBMETHOD = bdr:SubsettingImageVolume)
        ?ann :annInCollection ?R_RES .
        ?ann oa:hasTarget/oa:hasSource/:sourceImageSeq ?imgseq .
        FILTER (?imgseq <= ?I_SUBRANGELAST && ?imgseq >= ?I_SUBRANGEFIRST)
        ?ann ?annp ?anno .
        ?ann oa:hasTarget ?ant .
        ?ant ?antp ?anto .
        optional {
            FILTER(isBlank(?anto))
            ?anto ?antop ?antoo .
        }
        optional {
          ?ann oa:hasBody ?annb .
          ?annb ?annbp ?annbo .
          optional {
            ?annbo a :WorkLocation ;
            BIND(?annbo as ?annbloc )
            ?annbloc ?annblocp ?annbloco .
          }
          optional {
            ?annbo a adm:Assertion ;
            BIND(?annbo as ?annba )
            ?annba ?annbap ?annbao .
            optional {
               ?annba :workLocation ?annbaloc .
               ?annbaloc ?annbalocp ?annbaloco .
            }
          }
        }
    } union {
        FILTER (?R_SUBMETHOD = bdr:SubsettingChars)
        ?ann :annInCollection ?R_RES .
        ?ann oa:hasTarget/oa:hasSource ?source .
        ?source :sourcecharstart ?start ;
                :sourcecharend   ?end .
        FILTER (?start <= ?I_SUBRANGELAST && ?end >= ?I_SUBRANGEFIRST)
        ?ann ?annp ?anno .
        ?ann oa:hasTarget ?ant .
        ?ant ?antp ?anto .
        optional {
            FILTER(isBlank(?anto))
            ?anto ?antop ?antoo .
        }
        optional {
          ?ann oa:hasBody ?annb .
          ?annb ?annbp ?annbo .
          optional {
            ?annbo a :WorkLocation ;
            BIND(?annbo as ?annbloc )
            ?annbloc ?annblocp ?annbloco .
          }
          optional {
            ?annbo a adm:Assertion ;
            BIND(?annbo as ?annba )
            ?annba ?annbap ?annbao .
            optional {
               ?annba :workLocation ?annbaloc .
               ?annbaloc ?annbalocp ?annbaloco .
            }
          }
        }
    }
}
