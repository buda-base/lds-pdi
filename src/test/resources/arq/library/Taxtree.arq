#QueryScope=General
#QueryReturnType=Graph
#QueryResults=The tree of BDRC topics Taxonomy
#QueryParams=R_RES
#QueryUrl=/lib/Taxtree?R_RES=bdr:O9TAXTBRC201605_2741

construct {
  ?super bdo:taxHasSubClass ?sub .
  ?sub skos:prefLabel ?subl .
}
where{
  {
    ?R_RES bdo:taxHasSubClass* ?super .
    ?super bdo:taxHasSubClass ?sub .
    ?sub skos:prefLabel ?subl .
  }
  union
  {
    ?R_RES ^skos:topConceptOf/skos:narrower* ?super .
    ?super skos:narrower ?sub .
    ?sub skos:prefLabel ?subl .
  }
  union
  {
    ?R_RES ^skos:topConceptOf ?sub .
    BIND(?R_RES as ?super) .
    ?sub skos:prefLabel ?subl .
  }
  union
  {
    ?R_RES skos:prefLabel ?subl .
    BIND(?R_RES as ?sub)
  }
}
