#QueryScope=Work
#QueryReturnType=Graph
#QueryResults=A graph for the given work
#QueryParams=R_RES
#QueryUrl=/lib/resForMarc?R_RES=bdr:W23819

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct {
  ?R_RES ?p ?o .
  ?o ?op ?oo .
  ?t skos:prefLabel ?to .
  ?series skos:prefLabel ?seriesl .
  ?part skos:prefLabel ?partl .
  ?part bdo:workPartTreeIndex ?partti .
  ?R_RES tmp:workMarcLanguage ?mlang .
  ?R_RES tmp:workBCPLanguage ?bcplang .
  ?R_RES tmp:workOtherLanguage ?olang .
  ?pname ?pnamep ?pnameo .
  ?pevent ?peventp ?pevento .
  ?R_RES tmp:publishedYear ?publishedYear .
  ?R_RES tmp:completedYear ?completedYear .
  ?o tmp:birthYear ?birthYear .
  ?o tmp:deathYear ?deathYear .
} where {
  ?R_RES ?p ?o .
  optional {
    ?R_RES bdo:workIsAbout ?t .
    ?t skos:prefLabel ?to .
  }
  optional {
    ?R_RES bdo:workHasPart ?part .
    ?part skos:prefLabel ?partl ;
          bdo:workPartTreeIndex ?partti .
  }
  optional {
    ?R_RES bdo:workNumberOf ?series .
    ?series skos:prefLabel ?seriesl .
  }
  optional {
    ?p rdfs:subclassOf* :workCreator .
    ?o ?op ?oo .
    ?o bdo:personName ?pname .
    ?pname ?pnamep ?pnameo .
    optional {
      ?o bdo:personEvent ?pevent .
      ?pevent a :PersonBirth ;
        :onYear ?birthYear .
    }
    optional {
      ?o bdo:personEvent ?pevent2 .
      ?pevent2 a :PersonDeath ;
        :onYear ?deathYear .
    }
  }
  optional {
    FILTER(?p = :workLangScript)
    ?o :language/:langMARCCode ?mlang .
    ?o :language/:langBCP47Lang ?bcplang .
  }
  optional {
    FILTER(?p = :workOtherLangScript)
    ?o :language/:langMARCCode ?olang .
  }
  optional {
    ?R_RES :workEvent ?we .
    ?we a :PublishedEvent ;
      :onYear ?publishedYear .
  }
  optional {
    FILTER(isBlank(?o))
    ?o ?op ?oo .
  }
}
