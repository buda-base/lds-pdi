#QueryScope=Work
#QueryReturnType=Graph
#QueryResults=A graph for the given work
#QueryParams=R_RES
#QueryUrl=/lib/resForMarc?R_RES=bdr:W23819

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct {
  ?R_RES ?p ?o .
  ?R_RES tmp:status ?status .
  ?R_RES tmp:access ?access .
  ?R_RES tmp:license ?license .
  ?R_RES tmp:restrictedInChina ?ric .
  ?o ?op ?oo .
  ?t skos:prefLabel ?to .
  ?series skos:prefLabel ?seriesl .
  ?part ?partp ?parto .
  ?o bdo:personName ?pname .
  ?pname ?pnamep ?pnameo .
  ?pevent ?peventp ?pevento .
  ?R_RES tmp:publishedYear ?publishedYear .
  ?aac ?aacp ?aaco .
  ?o bdo:personEvent ?pevent .
  ?pevent ?peventp ?pevento .
  ?ols ?olsp ?olso .
  ?lango ?langop ?langoo .
  ?scripto ?scriptop ?scriptoo .
  ?ools ?oolsp ?oolso .
  ?id ?idp ?ido .
} where {
  {
    ?R_RES ?p ?o .
  }
  union {
    ?R_RES :instanceReproductionOf ?mw .
    ?mw ?p ?o .
  }
  union {
    ?R_RES :instanceOf ?wa .
    ?wa ?p ?o .
  }
  union {
    ?R_RES :instanceHasReproduction ?item .
    ?item a :ImageInstance ;
          ?p ?o .
  }
  union {
    ?R_RES bdo:instanceOf/bdo:workIsAbout ?t .
    ?t skos:prefLabel ?to .
  }
  union {
    ?R_RES :instanceHasReproduction? ?item .
    ?item a :ImageInstance .
    ?admItem adm:adminAbout ?item .
    ?admItem adm:access ?access .
    ?admItem adm:restrictedInChina ?ric .
    ?admItem adm:contentLegal/adm:license ?license .
    ?admWork adm:adminAbout ?R_RES .
    ?admWork adm:status ?status .
  }
  union {
    ?R_RES :instanceReproductionOf?/bdo:hasPart+ ?part .
    ?part ?partp ?parto .
    FILTER (?partp = skos:prefLabel || ?partp = bdo:hasPart || ?partp = bdo:partTreeIndex)
  }
  union {
    ?R_RES :instanceReproductionOf?/bdo:serialInstanceOf ?series .
    ?series skos:prefLabel ?seriesl .
  }
  union {
    ?R_RES bdo:instanceOf/bdo:creator ?aac .
    ?aac ?aacp ?aaco .
    ?aac bdo:agent ?o .
    ?o bdo:personName ?pname .
    ?pname ?pnamep ?pnameo .
  }
  union {
    ?R_RES bdo:instanceOf/bdo:creator ?aac .
    ?aac bdo:agent ?o .
    ?o bdo:personEvent ?pevent .
    ?pevent ?peventp ?pevento .
  }
  union {
    ?R_RES :instanceOf/:language ?lango .
    ?lango ?langop ?langoo .
  }
  union {
    ?R_RES :instanceReproductionOf?/bf:identifiedBy ?id .
    ?id ?idp ?ido .
  }
  union {
    ?R_RES :instanceReproductionOf?/:script ?scripto .
    ?scripto ?scriptop ?scriptoo .
  }
  union {
    ?R_RES :instanceReproductionOf?/:instanceEvent ?we .
    ?we a :PublishedEvent ;
      :onYear ?publishedYear .
  }
  union {
      ?R_RES :instanceReproductionOf?/:hasTitle ?o .
      ?o ?op ?oo .
  }
}
